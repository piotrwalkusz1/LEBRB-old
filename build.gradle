import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'org.springframework.boot' version '1.5.4.RELEASE' apply false
    id "com.github.johnrengelman.shadow" version "2.0.1" apply false
}

project(':legacy') {
    apply plugin: 'groovy'
    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8
    mainClassName = 'com.piotrwalkusz.lebrb.LEBRBApplication'

    repositories {
        mavenCentral()
    }

    sourceSets {
        jmh {
            java.srcDir 'src/jmh/java'
            resources.srcDir 'src/jmh/resources'
            compileClasspath = sourceSets.main.output + configurations.jmhCompile
            runtimeClasspath = compileClasspath + output + configurations.jmhRuntime
        }
        cucumber {
            groovy.srcDir 'src/cucumber/groovy'
            resources.srcDir 'src/cucumber/resources'
            compileClasspath = sourceSets.main.output + configurations.cucumberCompile
            runtimeClasspath = compileClasspath + output + configurations.cucumberRuntime
        }
    }

    configurations {
        jmhCompile.extendsFrom testCompile
        jmhRuntime.extendsFrom testRuntime
        cucumberCompile.extendsFrom testCompile
        cucumberRuntime.extendsFrom testRuntime
    }

    task jmhJar(type: ShadowJar) {
        manifest {
            attributes 'Main-Class': 'org.openjdk.jmh.Main'
        }
        from sourceSets.jmh.runtimeClasspath
        configurations = [project.configurations.jmhRuntime]
    }


    task jmh(type: JavaExec, dependsOn: jmhJar ) {
        classpath = jmhJar.outputs.files
        main = 'org.openjdk.jmh.Main'
    }

    task cucumber(dependsOn: [compileCucumberGroovy, processCucumberResources]) {
        doLast {
            javaexec {
                main = 'org.junit.runner.JUnitCore'
                args = ['com.piotrwalkusz.lebrb.CucumberTest']
                classpath = sourceSets.cucumber.runtimeClasspath + sourceSets.cucumber.output
            }
        }
    }

    shadowJar {
        manifest {
            attributes 'Main-Class': 'com.piotrwalkusz.lebrb.LEBRBApplication'
        }
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-actuator:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-actuator-docs:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-aop:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-cache:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-data-jpa:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-thymeleaf:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-security:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-validation:1.5.4.RELEASE')
        compile('org.springframework.boot:spring-boot-starter-web:1.5.4.RELEASE')
        compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity4', version: '2.1.3.RELEASE'
        compile('org.codehaus.groovy:groovy:2.4.11')
        compile('com.h2database:h2:1.4.196')
        compile('org.apache.pdfbox:pdfbox:2.0.6')
        compile 'javax.servlet:jstl:1.2'
        compile group: 'com.google.guava', name: 'guava', version: '22.0'
        compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'
        compile group: 'net.sf.jwordnet', name: 'jwnl', version: '1.4_rc3'
        compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.8.0'
        compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.8.0', classifier: 'models'
        runtime('org.springframework.boot:spring-boot-devtools:1.5.4.RELEASE')
        runtime('mysql:mysql-connector-java:6.0.6')
        testCompile('org.springframework.boot:spring-boot-starter-test:1.5.4.RELEASE')
        testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '4.2.3.RELEASE'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
        testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'
        testRuntime group: 'net.bytebuddy', name: 'byte-buddy', version: '1.7.1'
        jmhCompile(group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.19')
        jmhCompile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.19'
        cucumberCompile group: 'info.cukes', name: 'cucumber-java', version: '1.2.5'
        cucumberCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.5'
        cucumberCompile group: 'info.cukes', name: 'cucumber-spring', version: '1.2.5'
        cucumberCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.4.0'
    }
}